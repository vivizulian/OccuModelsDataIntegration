

    model {
    
    ###################################
    ###### ECOLOGICAL MODEL ###########
    ###################################

    # priors
    BETA0 ~ dnorm(0,0.001)
    BETA.CAMPO ~ dnorm(0,0.001)
    BETA.AGRI ~ dnorm(0,0.001)
    
    # LIKELIHOOD
    for (i in 1:nsite) {

      y1[i] ~ dbern(psi[i]) # Assuming y = z at site i
      mu[i] <- BETA0 + BETA.CAMPO * campo[i] + # occ model
                       BETA.AGRI * agri [i] 
      mu.lim[i] <- min(10, max(-10, mu[i]))  
      logit(psi[i]) <- mu.lim[i]

    }    

    # ======================================
    # goodness of fit
    # posterior predictive checks
    for (i in 1:nsite) {
    
        residual[i] <- y1[i]-psi[i] # Residuals for observed data
        predicted[i] <- psi[i] # Predicted values
        sq[i] <- pow(residual[i], 2) # Squared residuals for observed data
       
        # Generate replicate data and compute fit stats for them
        y.new[i] ~ dbern(psi[i]) # one new data set at each MCMC iteration
        sq.new[i] <- pow(y.new[i]-predicted[i], 2) # Squared residuals for new data
    }
        
    fit <- sum(sq[]) # Sum of squared residuals for actual data set
    fit.new <- sum(sq.new[]) # Sum of squared residuals for new data set
    test <- step(fit.new - fit) # Test whether new data set more extreme
    bpvalue <- mean(test) # Bayesian p-value
    
    # proportion of occupied municipalites
    pm <- sum(y1[])/nsite

    }## end of the model
    
