

    model {
    
    ###################################
    ###### ECOLOGICAL MODEL ###########
    ###################################

    # PRIORS
    BETA0 ~ dunif(-10,10)
    BETA.ALT ~ dunif(-10,10)
    
    
    # LIKELIHOOD
    for (i in 1:nsite) {

      z[i] ~ dbern(psi[i]) # True occupancy z at site i
  
      mu[i] <- BETA0 + BETA.ALT * altitude[i]

      # Keeping Winbugs on the track
      mu.lim[i] <- min(10, max(-10, mu[i]))  
      logit(psi[i]) <- mu.lim[i]

    }    

    ###################################
    ###### OBSERVATION MODELS #########
    ###################################

    ### GBIF 
    
    for (l in 1:nObsGB) {


        e1[l] <- ALPHA.GBIF * nSP.gbif[l]
        P1[l] <- 1-pow((1-0.5), e1[l])
        zP1[l] <- P1[l] * z[siteGB[l]]
        y.gbif[l] ~ dbern(zP1[l])


    }

    # PRIOR FOR RICHNESS EFFECT
    ALPHA.GBIF ~ dnorm(0,0.0001)I(0,10000)# truncated in positive values
    
    
    ### INATURALIST 
    
    for (r in 1:nObsIN) {


        e3[r] <- ALPHA.INAT * nSP.inat[r]
        P3[r] <- 1-pow((1-0.5), e3[r])
        zP3[r] <- P3[r] * z[siteIN[r]]
        y.inat[r] ~ dbern(zP3[r])


    }

    # PRIOR FOR RICHNESS EFFECT
    ALPHA.INAT ~ dnorm(0,0.0001)I(0,10000)# truncated in positive values
    
    
    ## EBIRD
  
    for (n in 1:nObsEB) {

        e4[n] <- ALPHA.DIST * dist [n] + ALPHA.DURATION * duration[n] + ALPHA.SP * nSP.eb[n]
        P4[n] <- 1-pow((1-0.5), e4[n])
        zP4[n] <- P4[n] * z[siteEB[n]]
        yEB[n] ~ dbern(zP4[n])

    }
    
    ALPHA.DIST ~ dnorm(0,0.0001)I(0,10000)
    ALPHA.DURATION ~ dnorm(0,0.0001)I(0,10000)
    ALPHA.SP ~ dnorm(0,0.0001)I(0,10000)
  

    ##############################
    #### DERIVED PARAMETERS ######
    ##############################
    
    #compute the mean detection probability of each dataset: 
    muP1 <- mean(P1[])
    #muP2 <- mean(P2[])
    muP3 <- mean(P3[])
    muP4 <- mean(P4[])
    
    
    ## Number of occupied municipalities (finite sample size)
    fs.z <- sum(z[])/nsite
    

    }## end of the model
    
